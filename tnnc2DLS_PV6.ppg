;****************************************************************
;
; Copyright (c) 2001 - 2003
; Bruker BioSpin MRI GmbH
; D-76275 Ettlingen, Germany
;
; All Rights Reserved
; 
; FLASH - a gradient echo imaging method
;
;****************************************************************
;
; d0 - TR padding
; d1 - T1 padding
; d2 - T2 padding
; d3 = PVM_RiseTime
; d4 = PVM_RampTime
; d8 = SCON / BLKTR_MAN (Amplifier preblanking)

#include <MRI.include>
preset off

define delay denab

define list<phase> phaselist = {$RFPhaseList}
define loopcounter lsize = {$ACQ_size[1]}

"denab =  d3 - de"
"l3 = lsize"

#include <PrepModulesHead.mod> 

subr FlowSatInit()

INIT_DEVICES

#define r2d ACQ_spatial_phase_1
#define r3d ACQ_spatial_phase_2

grad_zero_for_gs <r2d, r3d>

decr,   r2d.dec
        lo to decr times DS

        10u recph ph1
start,  10u

if (PVM_BlBloodOnOff == On)
{
  subr TriggerPhase()
}
subr BlBlood(ph0)
subr TriggerPhase()
subr SliceSelIr()
subr Tagging()

;take care of the dummy scan issue given the trigger-related tr changes

if "l25 > 0"
{ 
        100u
        21u
         2u    ; compensating the 1u for du 28 and 1u for l29gt0 
	goto slice
}
  ;L[0] = NDummyScans;
  ;L[2] = ACQ_dim>2? ACQ_size[2]:1;
  ;L[31] = PreBaseLineNum;
  ;L[30] = InterStimNum;
  ;L[29] = StimNum;
  ;L[28] = NEpochs;
  ;L[27] = PostBaseLineNum;
;trigger out through fMRI block design
if "l28==0"
{
	100u	du27
        21u
         2u    ; compensating the 1u for du 28 and 1u for l29gt0 
	goto slice
}
if "l31==0"
{
	if "l29>0"
	{
		5u 	TTL1_LOW
		5u 	TTL2_LOW
	        100u
		5u     TTL1_HIGH
		5u     TTL2_HIGH
                 1u      du29
		if "l29==0"
		{
			1u	du28
		}
		
        	goto l29gt0
	}
	if "l30>0"
	{
		100u	du30
        		21u
                 1u
	}
	

	l29gt0, 1u
	if "l30==0"  
	{
		0u	ru30  ; we tested it
		0u	ru29  ;we tested it
	}
}
if "l31>0"
{
	100u	du31
        21u
         2u  ; compensating the 1u for du 28 and 1u for l29gt0 
}


;----------------------------------start of the main loop ----------
slice, 	10u fq8(receive):f1
	20u reload B0

;----------------------------------preparation modules -------------
subr TriggerSlice()
subr FlowSat()
subr FovSat()
subr MagTrans()
subr FatSup()

        d6	grad_ramp{0, 0, g9}
;----------------------------------slice selection------------------	
	d3 	grad_ramp{0, 0, g0} fq1:f1
        d8	gatepulse 1
	(p0:sp0 phaselist):f1
	d4 	grad_off
;----------------------slice rephase, read dephase, phase encoding----
	d10 	grad_ramp{g2, r2d*g3, g1+r3d*g4}
	d2 	grad_off
;----------------------------------frequency encoding---------------
        denab   grad_ramp{g5, 0, 0} 
	ADC_INIT_B(NOPH, phaselist)  ;ph1 is used (see recph)
        aqq	ADC_START	
;----------------------------------read spoiler + phase encoding----
        d11     grad_ramp{g6, r2d*g7, r3d*g8} ; 2nd and 3rd dims
	d12     grad_ramp{g6, 0, 0}
	d3	grad_off
        d0	ADC_END
;----------------------------------slice and movie loop-----------------------	
subr FlowSatInc()
if(NI > 1)
{
	2.5u grad_matrix.inc
	;lo to slice times NI
        lo to start times l10             ;PVM_NMovieFrames 
}
    
        lo to slice times NSLICES             

        d20
        1u  phaselist.inc    ;phase list for RF spoiling
;--------------------------- dummy loop ----------------------------
       "l25=l25-1"
if "l25>=0" goto start ;make sure thta the dummy scan is not going to cause trigger number change

;----------------------------------averaging loop-------------------
if(NA > 1)
{
	lo to start times NA
}
if "l25 <= 0"
{
       r2d.inc
       r3d.inc
}
;----------------------------------2d loop--------------------------
	lo to start times l3
;----------------------------------3d loop--------------------------
	lo to start times l2
        2.5u
        lo to start times NAE
        2.5u
        subr Evolution()
        2.5u
        lo to start times NR
SETUP_GOTO(start)

exit

ph0 = 0
ph1 = 0 




	





